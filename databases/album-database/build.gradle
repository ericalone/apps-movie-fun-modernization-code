import org.flywaydb.gradle.FlywayExtension
import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
    dependencies {
        classpath "mysql:mysql-connector-java:$mysqlVersion"
    }
}

plugins {
    id "org.flywaydb.flyway" version "4.0.3"
}

apply from: "../db.gradle"

flyway {
    url = "jdbc:mysql://localhost:3306/albums?useSSL=false"
    user = "root"
    outOfOrder = false
}

def waitUntilSocketIsOpen(port) {
    def attempts = 0
    def open = false


    while (!open && attempts <= 20) {
        try {
            def socket = new Socket("localhost", port)
            open = true
        } catch (IOException e) {
            attempts += 1
            Thread.sleep(200)
        }
        if (attempts > 20) {
            throw GradleException("Timed out waiting for SSH tunnel to be open")
        }
    }
}

def tunnelProcess

task openTunnel {
    doLast {
        println 'Opening Tunnel'
        Thread.start {
            tunnelProcess = "cf ssh -N -L 63306:${getMysqlHost("album-service")}:3306 album-service".execute()
        }

        waitUntilSocketIsOpen(63306)
    }
}

task closeTunnel {
    doLast {
        println 'Closing Tunnel'
        tunnelProcess.destroy()
    }
}

task cfMigrate(type: FlywayMigrateTask, dependsOn: openTunnel) {
    if (System.env.CF_MIGRATE) {
        extension = setupProdFlywayExtension(new FlywayExtension(), "album-service")
    }
}

cfMigrate.finalizedBy closeTunnel
